defmodule SymphonyElixir.AgentRunner do
  @moduledoc """
  Executes a single tracker work item in its workspace with Codex.
  """

  require Logger
  alias SymphonyElixir.Codex.AppServer

  alias SymphonyElixir.{
    Config,
    FreshThreadHandoff,
    PromptArchive,
    PromptBuilder,
    PromptContext,
    Tracker,
    Tracker.Issue,
    Workflow,
    Workspace
  }

  @type worker_host :: String.t() | nil

  @doc false
  @spec continue_with_issue_for_test(Issue.t(), ([String.t()] -> term())) ::
          {:continue, Issue.t()} | {:done, Issue.t()} | {:state_changed, Issue.t()} | {:error, term()}
  def continue_with_issue_for_test(%Issue{} = issue, issue_state_fetcher)
      when is_function(issue_state_fetcher, 1) do
    continue_with_issue?(issue, issue_state_fetcher)
  end

  @spec run(map(), pid() | nil, keyword()) :: :ok | no_return()
  def run(issue, codex_update_recipient \\ nil, opts \\ []) do
    # The orchestrator owns host retries so one worker lifetime never hops machines.
    worker_host = selected_worker_host(Keyword.get(opts, :worker_host), Config.settings!().worker.ssh_hosts)

    Logger.info("Starting agent run for #{issue_context(issue)} worker_host=#{worker_host_for_log(worker_host)}")

    case run_on_worker_host(issue, codex_update_recipient, opts, worker_host) do
      :ok ->
        :ok

      {:error, reason} ->
        Logger.error("Agent run failed for #{issue_context(issue)}: #{inspect(reason)}")
        raise RuntimeError, "Agent run failed for #{issue_context(issue)}: #{inspect(reason)}"
    end
  end

  defp run_on_worker_host(issue, codex_update_recipient, opts, worker_host) do
    Logger.info("Starting worker attempt for #{issue_context(issue)} worker_host=#{worker_host_for_log(worker_host)}")

    case Workspace.create_for_issue(issue, worker_host) do
      {:ok, workspace} ->
        send_worker_runtime_info(codex_update_recipient, issue, worker_host, workspace)

        try do
          with :ok <- Workspace.run_before_run_hook(workspace, issue, worker_host) do
            run_codex_turns(workspace, issue, codex_update_recipient, opts, worker_host)
          end
        after
          Workspace.run_after_run_hook(workspace, issue, worker_host)
        end

      {:error, reason} ->
        {:error, reason}
    end
  end

  defp codex_message_handler(recipient, issue) do
    fn message ->
      send_codex_update(recipient, issue, message)
    end
  end

  defp send_codex_update(recipient, %Issue{id: issue_id}, message)
       when is_binary(issue_id) and is_pid(recipient) do
    send(recipient, {:codex_worker_update, issue_id, message})
    :ok
  end

  defp send_codex_update(_recipient, _issue, _message), do: :ok

  defp send_worker_runtime_info(recipient, %Issue{id: issue_id}, worker_host, workspace)
       when is_binary(issue_id) and is_pid(recipient) and is_binary(workspace) do
    send(
      recipient,
      {:worker_runtime_info, issue_id,
       %{
         worker_host: worker_host,
         workspace_path: workspace
       }}
    )

    :ok
  end

  defp send_worker_runtime_info(_recipient, _issue, _worker_host, _workspace), do: :ok

  defp run_codex_turns(workspace, issue, codex_update_recipient, opts, worker_host) do
    max_turns = Keyword.get(opts, :max_turns, Config.settings!().agent.max_turns)
    issue_state_fetcher = Keyword.get(opts, :issue_state_fetcher, &Tracker.fetch_issues_by_ids/1)
    turn_opts = Keyword.put(opts, :worker_host, worker_host)

    with {:ok, session} <- AppServer.start_session(workspace, worker_host: worker_host) do
      try do
        do_run_codex_turns(
          session,
          workspace,
          issue,
          codex_update_recipient,
          turn_opts,
          issue_state_fetcher,
          1,
          max_turns
        )
      after
        AppServer.stop_session(session)
      end
    end
  end

  defp do_run_codex_turns(app_session, workspace, issue, codex_update_recipient, opts, issue_state_fetcher, turn_number, max_turns) do
    turn_started_at_ms = System.monotonic_time(:millisecond)
    worker_host = Keyword.get(opts, :worker_host)

    with {:ok, phase_context} <- PromptContext.load(workspace, issue, worker_host),
         {prompt, phase_context} <-
           issue
           |> build_turn_prompt(opts, turn_number, max_turns)
           |> PromptContext.append(phase_context),
         :none <-
           FreshThreadHandoff.consume(
             workspace,
             issue,
             phase_context && phase_context.contract_hash,
             worker_host
           ),
         :ok <- archive_turn_prompt(codex_update_recipient, issue, opts, turn_number, prompt, phase_context),
         {:ok, turn_session} <-
           AppServer.run_turn(
             app_session,
             prompt,
             issue,
             on_message: codex_message_handler(codex_update_recipient, issue)
           ) do
      Logger.info("Completed agent run for #{issue_context(issue)} session_id=#{turn_session[:session_id]} workspace=#{workspace} turn=#{turn_number}/#{max_turns}")

      case FreshThreadHandoff.consume(
             workspace,
             issue,
             phase_context && phase_context.contract_hash,
             worker_host
           ) do
        {:ok, receipt} ->
          finish_for_fresh_thread(issue, receipt)

        :none ->
          continue_after_turn(
            %{
              app_session: app_session,
              workspace: workspace,
              issue: issue,
              recipient: codex_update_recipient,
              opts: opts,
              issue_state_fetcher: issue_state_fetcher,
              turn_number: turn_number,
              max_turns: max_turns,
              turn_started_at_ms: turn_started_at_ms
            },
            turn_session
          )

        {:error, reason} ->
          {:error, reason}
      end
    else
      {:ok, receipt} -> finish_for_fresh_thread(issue, receipt)
      {:error, reason} -> {:error, reason}
    end
  end

  defp finish_for_fresh_thread(issue, receipt) do
    Logger.info(
      "Consumed fresh-thread handoff for #{issue_context(issue)} " <>
        "reason=#{receipt.reason} contract_hash=#{receipt.contract_hash}; " <>
        "returning control to orchestrator for a fresh agent session"
    )

    :ok
  end

  defp continue_after_turn(run, turn_session) do
    %{
      app_session: app_session,
      workspace: workspace,
      issue: issue,
      recipient: codex_update_recipient,
      opts: opts,
      issue_state_fetcher: issue_state_fetcher,
      turn_number: turn_number,
      max_turns: max_turns,
      turn_started_at_ms: turn_started_at_ms
    } = run

    case continue_with_issue?(issue, issue_state_fetcher) do
      {:continue, refreshed_issue} when turn_number < max_turns ->
        Logger.info("Continuing agent run for #{issue_context(refreshed_issue)} after normal turn completion turn=#{turn_number}/#{max_turns}")

        throttle_continuation_turn(refreshed_issue, opts, turn_started_at_ms)

        do_run_codex_turns(
          app_session,
          workspace,
          refreshed_issue,
          codex_update_recipient,
          opts,
          issue_state_fetcher,
          turn_number + 1,
          max_turns
        )

      {:continue, refreshed_issue} ->
        Logger.info("Reached agent.max_turns for #{issue_context(refreshed_issue)} with issue still active; returning control to orchestrator")

        :ok

      {:state_changed, refreshed_issue} ->
        Logger.info(
          "Issue changed active workflow state for #{issue_context(refreshed_issue)} " <>
            "session_id=#{turn_session[:session_id]} previous_state=#{inspect(issue.state)} state=#{inspect(refreshed_issue.state)}; " <>
            "returning control to orchestrator for a fresh agent session"
        )

        :ok

      {:done, _refreshed_issue} ->
        :ok

      {:rate_limited, reason} ->
        Logger.warning("Linear rate limited while refreshing #{issue_context(issue)} turn=#{turn_number}/#{max_turns}: #{inspect(reason)}; returning control to orchestrator")

        :ok

      {:error, reason} ->
        {:error, reason}
    end
  end

  # Fast workpad loops can burn the tracker's hourly API quota (each turn costs
  # a state refresh here plus the agent's own tracker traffic), so enforce a
  # minimum interval between continuation turn starts when configured.
  defp throttle_continuation_turn(issue, opts, turn_started_at_ms) do
    min_interval_ms =
      Keyword.get(
        opts,
        :continuation_min_turn_interval_ms,
        Config.settings!().agent.continuation_min_turn_interval_ms
      )

    if is_integer(min_interval_ms) and min_interval_ms > 0 do
      elapsed_ms = System.monotonic_time(:millisecond) - turn_started_at_ms
      wait_ms = min_interval_ms - elapsed_ms

      if wait_ms > 0 do
        Logger.info("Throttling continuation turn for #{issue_context(issue)}; waiting #{wait_ms}ms to respect continuation_min_turn_interval_ms=#{min_interval_ms}")

        Process.sleep(wait_ms)
      end
    end

    :ok
  end

  # The dashboard is the only place a human can see what the agent was told, so
  # the prompt is archived before the turn starts rather than after it settles;
  # a turn that dies mid-flight still leaves its instructions behind.
  defp archive_turn_prompt(recipient, %Issue{id: issue_id} = issue, opts, turn_number, prompt, phase_context)
       when is_binary(issue_id) do
    case PromptArchive.record(issue, turn_number, prompt,
           attempt: Keyword.get(opts, :attempt),
           workflow_file: Workflow.workflow_file_path(),
           phase_context: phase_context
         ) do
      {:ok, ref} ->
        if is_pid(recipient), do: send(recipient, {:turn_prompt_archived, issue_id, ref})
        :ok

      {:error, reason} ->
        Logger.warning("Skipped prompt archive for #{issue_context(issue)} turn=#{turn_number}: #{inspect(reason)}")
        :ok
    end
  end

  defp archive_turn_prompt(_recipient, _issue, _opts, _turn_number, _prompt, _phase_context), do: :ok

  defp build_turn_prompt(issue, opts, 1, _max_turns), do: PromptBuilder.build_prompt(issue, opts)

  defp build_turn_prompt(_issue, _opts, turn_number, max_turns) do
    """
    Continuation guidance:

    - The previous Codex turn completed normally, but the tracker work item is still in an active state.
    - This is continuation turn ##{turn_number} of #{max_turns} for the current agent run.
    - Resume from the current workspace and workpad state instead of restarting from scratch.
    - The original task instructions and prior turn context are already present in this thread, so do not restate them before acting.
    - Focus on the remaining ticket work and do not end the turn while the issue stays active unless you are truly blocked.
    """
  end

  defp continue_with_issue?(%Issue{id: issue_id} = issue, issue_state_fetcher) when is_binary(issue_id) do
    case issue_state_fetcher.([issue_id]) do
      {:ok, [%Issue{} = refreshed_issue | _]} ->
        cond do
          not active_issue_state?(refreshed_issue.state) or not issue_routable?(refreshed_issue) ->
            {:done, refreshed_issue}

          same_issue_state?(issue.state, refreshed_issue.state) ->
            {:continue, refreshed_issue}

          true ->
            {:state_changed, refreshed_issue}
        end

      {:ok, []} ->
        {:done, issue}

      {:error, {:linear_rate_limited, _remaining_ms} = reason} ->
        {:rate_limited, reason}

      {:error, reason} ->
        {:error, {:issue_state_refresh_failed, reason}}
    end
  end

  defp continue_with_issue?(issue, _issue_state_fetcher), do: {:done, issue}

  @doc false
  @spec throttle_continuation_turn_for_test(Issue.t(), keyword(), integer()) :: :ok
  def throttle_continuation_turn_for_test(%Issue{} = issue, opts, turn_started_at_ms)
      when is_list(opts) and is_integer(turn_started_at_ms) do
    throttle_continuation_turn(issue, opts, turn_started_at_ms)
  end

  @doc false
  @spec continue_with_issue_for_test?(Issue.t(), ([String.t()] -> term())) ::
          {:continue, Issue.t()}
          | {:state_changed, Issue.t()}
          | {:done, Issue.t()}
          | {:rate_limited, term()}
          | {:error, term()}
  def continue_with_issue_for_test?(%Issue{} = issue, issue_state_fetcher)
      when is_function(issue_state_fetcher, 1) do
    continue_with_issue?(issue, issue_state_fetcher)
  end

  defp active_issue_state?(state_name) when is_binary(state_name) do
    normalized_state = normalize_issue_state(state_name)

    Config.settings!().tracker.active_states
    |> Enum.any?(fn active_state -> normalize_issue_state(active_state) == normalized_state end)
  end

  defp active_issue_state?(_state_name), do: false

  defp issue_routable?(%Issue{} = issue) do
    tracker = Config.settings!().tracker
    Issue.routable?(issue, tracker.required_labels, tracker.active_labels)
  end

  defp same_issue_state?(left, right) when is_binary(left) and is_binary(right) do
    normalize_issue_state(left) == normalize_issue_state(right)
  end

  defp same_issue_state?(_left, _right), do: false

  defp selected_worker_host(nil, []), do: nil

  defp selected_worker_host(preferred_host, configured_hosts) when is_list(configured_hosts) do
    hosts =
      configured_hosts
      |> Enum.map(&String.trim/1)
      |> Enum.reject(&(&1 == ""))
      |> Enum.uniq()

    case preferred_host do
      host when is_binary(host) and host != "" -> host
      _ when hosts == [] -> nil
      _ -> List.first(hosts)
    end
  end

  defp worker_host_for_log(nil), do: "local"
  defp worker_host_for_log(worker_host), do: worker_host

  defp normalize_issue_state(state_name) when is_binary(state_name) do
    state_name
    |> String.trim()
    |> String.downcase()
  end

  defp issue_context(%Issue{id: issue_id, identifier: identifier}) do
    "issue_id=#{issue_id} issue_identifier=#{identifier}"
  end
end
